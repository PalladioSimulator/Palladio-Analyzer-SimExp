grammar org.palladiosimulator.simexp.dsl.kmodel.Kmodel with org.eclipse.xtext.common.Terminals

generate kmodel "http://www.palladiosimulator.org/simexp/dsl/kmodel/Kmodel"

KModel:
	(variables+=VariableDeclarationStatement)*
	(actions+=ActionDeclaration)*
	(statements+=IfStatement)*;
	
ActionDeclaration:
	'action' name=ID '(' parameter=Parameter ')' ';';
	
Parameter: dataType=DataType name=ID;

VariableDeclarationStatement returns Statement: 
	'var' var=Variable ';';
	
ActionStatement returns Statement:
	action=[ActionDeclaration] '(' argument=Expression ')' ';';
	
IfStatement returns Statement: 
	'if' '(' condition=Expression ')' '{' body=Body '}';

Body:
	{Body} statements+=(ActionStatement | IfStatement)*;

Expression: 
	variable=[Variable] | constant=Constant;	

Variable: 
	dataType=DataType name=ID;

Constant returns Expression: 
	{BoolConstant} value=BOOL
	| {IntConstant} value=INT 
	| {FloatConstant} value=FLOAT 
	| {StringConstant} value=STRING;

enum DataType: BOOL = 'bool' | INT = 'int' | FLOAT = 'float' | STRING = 'string';

terminal BOOL: 'true' | 'false';

terminal FLOAT: INT '.' INT;