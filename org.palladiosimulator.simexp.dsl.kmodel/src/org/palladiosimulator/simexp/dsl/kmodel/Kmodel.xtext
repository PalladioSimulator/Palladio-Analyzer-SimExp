grammar org.palladiosimulator.simexp.dsl.kmodel.Kmodel with org.eclipse.xtext.common.Terminals

generate kmodel "http://www.palladiosimulator.org/simexp/dsl/kmodel/Kmodel"

KModel:
	(fields+=Field)*
	(actions+=Action)*
	(statements+=IfStatement)*;
	
Field:
	Constant | Variable | Probe;
	
Constant returns Field:
	{Constant} 'const' dataType=DataType name=ID '=' value=Expression ';';	
	
Variable returns Field:
	{Variable} 'var' dataType=DataType name=ID ';';	

Probe returns Field:
	{Probe} 'probe' id=ID 'as' dataType=DataType name=ID ';';
	
Action:
	{Action} 'action' name=ID '(' parameter=Parameter ')' ';';
	
Parameter: paramType=ParamType dataType=DataType name=ID;

Statement:
	ActionCall | IfStatement;

ActionCall returns Statement:
	actionRef=[Action] '(' argument=Expression ')' ';';
	
IfStatement returns Statement: 
	'if' '(' condition=Expression ')' '{' (statements+=Statement)* '}';

Expression: Disjunction;

Disjunction returns Expression:
    Conjunction ({Disjunction.left=current} '|' right=Conjunction)*;
    
Conjunction returns Expression:
	Equality ({Conjunction.left=current} '&' right=Equality)*;
	
Equality returns Expression:
	Negation ({Equality.left=current} op=('==' | '!=') right=Negation)*;
	
Negation returns Expression:
	{Negation} (negated?='!')? expr=Comparison;	
	
Comparison returns Expression:
	Addition ({Comparison.left=current} op=('<' | '<=' | '>=' | '>') right=Addition)?;	
	
Addition returns Expression:
	AdditiveInversion ({Addition.left=current} op=('+' | '-') right=Multiplication)*;
	
AdditiveInversion returns Expression:
	{AdditiveInversion} (inverted?='-')? expr=Multiplication;
	
Multiplication returns Expression:
	Term ({Multiplication.left=current} op=('*' | '/') right=Term)*;			

Term returns Expression: 
	fieldRef=[Field] | literal=Literal | '(' expr=Expression ')';	

Literal returns Expression: 
	  {BoolLiteral} value=BOOL
	| {IntLiteral} value=INT 
	| {FloatLiteral} value=FLOAT
	| {StringLiteral} value=STRING;

enum DataType: BOOL = 'bool' | INT = 'int' | FLOAT = 'float' | STRING = 'string';

enum ParamType: PARAMETER = 'param' | VARIABLE = 'var';

terminal BOOL: 'true' | 'false';

terminal FLOAT: INT '.' INT;