grammar org.palladiosimulator.simexp.dsl.kmodel.Kmodel with org.eclipse.xtext.common.Terminals

generate kmodel "http://www.palladiosimulator.org/simexp/dsl/kmodel/Kmodel"

KModel:
	(constants+=ConstantDeclaration)*
	(variables+=VariableDeclaration)*
	(probes+=ProbeDeclaration)*
	(actions+=ActionDeclaration)*
	(statements+=IfStatement)*;
	
ConstantDeclaration returns Field:
	{Constant} 'const' dataType=DataType name=ID '=' value=Expression ';';	
	
VariableDeclaration returns Field:
	{Variable} 'var' dataType=DataType name=ID ';';	

ProbeDeclaration returns Field:
	{Probe} 'probe' dataType=DataType id=ID 'as' name=ID ';';
	
ActionDeclaration:
	'action' name=ID '(' parameter=Parameter ')' ';';
	
Parameter: paramType=ParamType dataType=DataType name=ID;
	
ActionStatement returns Statement:
	action=[ActionDeclaration] '(' argument=Expression ')' ';';
	
IfStatement returns Statement: 
	'if' '(' condition=Expression ')' '{' body=Body '}';

Body:
	{Body} statements+=(ActionStatement | IfStatement)*;

Expression: 
	field=[Field] | literal=Literal;	

Literal returns Expression: 
	  {BoolLiteral} value=BOOL
	| {IntLiteral} value=INT 
	| {FloatLiteral} value=FLOAT
	| {StringLiteral} value=STRING;

enum DataType: BOOL = 'bool' | INT = 'int' | FLOAT = 'float' | STRING = 'string';

enum ParamType: PARAMETER = 'param' | VARIABLE = 'var';

terminal BOOL: 'true' | 'false';

terminal FLOAT: INT '.' INT;